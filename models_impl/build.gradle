plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion project.compile
    buildToolsVersion project.tools

    defaultConfig {
        minSdkVersion project.min
        targetSdkVersion project.target

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField "int", "UPDATE_INTERVAL", "600"
        buildConfigField "int", "TRACKING_SENSIVITY", "60"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "int", "UPDATE_INTERVAL", "5"
            buildConfigField "int", "TRACKING_SENSIVITY", "10"
        }
    }

    flavorDimensions "provider"
    productFlavors {
        gps {
            dimension "provider"
            buildConfigField "String", "LOCATION_PROVIDER", "\"gps\""
        }
        network {
            dimension "provider"
            buildConfigField "String", "LOCATION_PROVIDER", "\"network\""
        }
        passive {
            dimension "provider"
            buildConfigField "String", "LOCATION_PROVIDER", "\"passive\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation project(path: ':models')

    //AndroidX
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'

    //RxJava
    implementation 'io.reactivex.rxjava3:rxjava:3.1.3'

    //Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Google Play Services
    implementation 'com.google.android.gms:play-services-location:20.0.0'

    //Firebase
    implementation 'com.google.firebase:firebase-firestore:24.1.2'
    implementation 'com.google.firebase:firebase-auth:21.0.5'

    //JUnit, Mockito
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.2.4'
    testImplementation 'org.mockito:mockito-inline:2.8.47'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}